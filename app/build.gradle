apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// load properties related to the remote service
def apiProperties = new Properties()
def apiPropertiesFile = rootProject.file('api.properties')
if (apiPropertiesFile.exists()) {
    apiProperties.load(new FileInputStream(apiPropertiesFile))
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"
    // ndkVersion "22.1.7171670"

    defaultConfig {
        applicationId "com.android.skeleton"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        // enable multiple dex files to support java 8 in any sdk
        // (d8 implements missing java 8 apis in separate dex files)
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // to use vector drawable compat
        vectorDrawables.useSupportLibrary true

        javaCompileOptions {
            // to export the db schema
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":"$projectDir/schemas".toString()]
            }
        }

        buildConfigField("String", "appId", apiProperties['appId'])
        buildConfigField("String", "appKey", apiProperties['appKey'])
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            ndk { debugSymbolLevel 'FULL' }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-debug.pro'
        }
    }

    compileOptions {
        // enable support for java 11 new apis
        coreLibraryDesugaringEnabled true
        // set java compatibility to java 11
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        // required to use byViewModels kotlin property delegate from fragment-ktx
        jvmTarget = "11"
    }
}

dependencies {

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.21"

    // android ktx
    implementation "androidx.core:core-ktx:1.6.0"

    // support libraries
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    // support java 8 in any sdk version
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

    // ui
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"

    // preference
    implementation "androidx.preference:preference-ktx:1.1.1"

    // image loading, display, etc
    // implementation "com.facebook.fresco:fresco:2.3.0"

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0-alpha03"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"

    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1"

    // room db
    implementation "androidx.room:room-runtime:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"
    implementation "androidx.room:room-ktx:2.3.0"

    // dependency injection
    implementation "com.google.dagger:hilt-android:2.38.1"
    kapt "com.google.dagger:hilt-android-compiler:2.38.1"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"

    // logging timber
    implementation "com.jakewharton.timber:timber:4.7.1"

    // json tools
    implementation("com.squareup.moshi:moshi-kotlin:1.12.0")
    implementation("org.jetbrains.kotlin:kotlin-reflect:1.5.31")

    // remote service
    implementation("com.squareup.retrofit2:converter-moshi:2.9.0")

    // firebase
    implementation platform("com.google.firebase:firebase-bom:28.4.2")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // testing
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "com.google.dagger:hilt-android-testing:2.38.1"
    androidTestImplementation "androidx.test:rules:1.4.0"
}
